#Область ПрограммныйИнтерфейс
Функция СведенияОВнешнейОбработке() Экспорт		
	Возврат ОписаниеСервиса().СведенияОВнешнейОбработке;
КонецФункции 

Функция ОписаниеСервиса() Экспорт 
	ОписаниеСервиса = ДанныеСервиса.Инициализация()
		.Имя("Test Swagger")
		.Описание("Тестовая обработка для Swagger", "Эта обработка показывает возможности документирования сервисов")
		.Префикс("test/v1")
		.Версия("1.0")
		.Контакт("NAvdeyev", "", "navdeyev@kpmg.kz")
		.Сервер("dev.kpmg.kz", "Сервер для разработки")
			.ПараметрыСервера()
				.НовыйИмяПользователяСервера()
					.ИмяПользователяСервера("Guest")
					.ИмяПользователяСервера("Test")
					.ИмяПоУмолчанию("Test")
					.ОписаниеИмени("Описание для имени пользователя сервера") 
				.НовыйПортСервера()
					.ПортСервера("443")
					.ПортСервера("8443")
					.ПортСервера("80")    
					.ПортПоУмолчанию("443")
					.ОписаниеПорта("Описание для портов")
		.Сервис("Ping", "Пинг", "Сервис вернет универсальную дату")
			.Операция("get", "", "")
		.Сервис("HelloWorld", "Привет Мир", "Метод вернет приветственое сообщение")
			.Операция("get", "", "")
			.Операция("post", "", "") 
				.Параметр("name", "query", Истина, Ложь)
		.Создать();
		
	Возврат ОписаниеСервиса;
КонецФункции 
#КонецОбласти

#Область СлужебныйПрограмныйИнтерфейс
Функция ВыполнитьКоманду(Идентификатор, ПараметрыВыполнения) Экспорт
	Если Идентификатор = "/HelloWorld" Тогда
		Возврат ПриветМир(ПараметрыВыполнения);
	ИначеЕсли Идентификатор = "/Ping" Тогда
		Возврат Пинг(ПараметрыВыполнения);	
	КонецЕсли;	
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции
Функция Пинг(ПараметрыВыполнения) 
	HTTPОтвет = Новый HTTPСервисОтвет(200);
	HTTPОтвет.УстановитьТелоИзСтроки(ТекущаяУниверсальнаяДатаВМиллисекундах()); 
	Возврат HTTPОтвет;			
КонецФункции
 
Функция ПриветМир(ПараметрыВыполнения)
	HTTPОтвет = Новый HTTPСервисОтвет(200);
	Шаблон = "Привет, %1!";     
	Запрос = ПараметрыВыполнения.HTTPЗапрос;

	Если ПараметрыВыполнения.Метод = "GET" Тогда
		Тело = СтрШаблон(Шаблон, "Человек");
	ИначеЕсли ПараметрыВыполнения.Метод = "POST" Тогда 
		ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
		Чтение = Новый ЧтениеJSON;
		Чтение.УстановитьСтроку(ТелоЗапроса);
		XDTOОбъект = ФабрикаXDTO.ПрочитатьJSON(Чтение);
		Чтение.Закрыть();
		Тело = СтрШаблон(Шаблон, XDTOОбъект.name);
	Иначе
		HTTPОтвет = Новый HTTPСервисОтвет(404);
		Тело = "Нет обработчика запрашиваемого метода";
	КонецЕсли;

	HTTPОтвет.УстановитьТелоИзСтроки(Тело);
	Возврат HTTPОтвет;			
КонецФункции      
#КонецОбласти