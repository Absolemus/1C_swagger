#Область Переменные                        
Перем МетодОбработки;
Перем СведенияОВнешнейОбработке;

Перем Префикс;
Перем Серверы;
Перем Контакт;
Перем Сервисы;
Перем Лицензия;  
Перем ИмяСервиса;
Перем Информация;   
Перем ТелоЗапроса;
Перем ПортСервера;
Перем ПортыСервера; 
Перем ГлавныйОбъект; 
Перем ИмяПользователя;
Перем СтруктураСервиса; 
Перем СтруктураСервера;
Перем ПараметрыСервера; 
Перем СтруктураОперации;
Перем ИменаПользователей;
Перем СтруктураПараметра;
#КонецОбласти 

#Область ПрограмныйИнтерфейс
Функция Инициализация() Экспорт
	ГлавныйОбъект = Новый Структура;
	ГлавныйОбъект.Вставить("openapi", "3.1.0");
	
	Информация = Новый Структура;
	ГлавныйОбъект.Вставить("info", Информация);
	
	Сервисы = Новый Соответствие;
	ГлавныйОбъект.Вставить("paths", Сервисы);

	Серверы = Новый Массив; 
	ГлавныйОбъект.Вставить("servers", Серверы); 
	
	СведенияОВнешнейОбработке = ДополнительныеОтчетыИОбработки.СведенияОВнешнейОбработке();
	СведенияОВнешнейОбработке.Вид = ДополнительныеОтчетыИОбработкиКлиентСервер.ВидОбработкиHTTPСервис(); 
	СведенияОВнешнейОбработке.БезопасныйРежим = Ложь;  
	
	Возврат ЭтотОбъект;
КонецФункции  
#КонецОбласти

#Область СлужебныйПрограмныйИнтерфейс
#Область Информация
Функция Имя(Имя) Экспорт
	Информация.Вставить("title", Имя);
	СведенияОВнешнейОбработке.Наименование = Имя;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Описание(КраткоеОписание, ПолноеОписание) Экспорт
	Информация.Вставить("summary", КраткоеОписание);
	Информация.Вставить("description", ПолноеОписание);
	СведенияОВнешнейОбработке.Информация = ПолноеОписание;
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ПолитикаИспользования(Описание) Экспорт
	Информация.Вставить("termsOfService", Описание);
	
	Возврат ЭтотОбъект;
КонецФункции                

Функция Версия(Версия) Экспорт
	Информация.Вставить("version", Версия);
	СведенияОВнешнейОбработке.Версия = Версия;
	Возврат ЭтотОбъект;
КонецФункции                

Функция Контакт(Имя, Ссылка, Почта) Экспорт
	Контакт = Новый Структура("name, url, email", Имя, Ссылка, Почта);
	Информация.Вставить("contact", Контакт);
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Лицензия(Имя, Идентификатор, Ссылка) Экспорт
	Лицензия = Новый Структура("name, identifier, url", Имя, Идентификатор, Ссылка);
	Информация.Вставить("license", Лицензия);
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Префикс(Путь) Экспорт
	Префикс = Путь;
	
	Возврат ЭтотОбъект;
КонецФункции
#КонецОбласти

#Область Серверы
Функция Сервер(Путь, Описание) Экспорт 
	СтруктураСервера = Новый Структура("url, description", Путь, Описание);
	Серверы.Добавить(СтруктураСервера);
	
	Возврат ЭтотОбъект;
КонецФункции  

Функция ПараметрыСервера() Экспорт           
	ПараметрыСервера = Новый Структура;
	СтруктураСервера.Вставить("variables", ПараметрыСервера);	 
	
	Возврат ЭтотОбъект;
КонецФункции

Функция НовыйИмяПользователяСервера() Экспорт      
	ИмяПользователя = Новый Структура;
	ИменаПользователей = Новый Массив;
	ИмяПользователя.Вставить("enum", ИменаПользователей);
	ПараметрыСервера.Вставить("username", ИмяПользователя);	
	
	Возврат ЭтотОбъект;
КонецФункции      

Функция ИмяПользователяСервера(Имя) Экспорт
	ИменаПользователей.Добавить(Имя);
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ИмяПоУмолчанию(Имя) Экспорт
	ИмяПользователя.Вставить("default", Имя);
	
	Возврат ЭтотОбъект;
КонецФункции

Функция ОписаниеИмени(Описание) Экспорт
	ИмяПользователя.Вставить("description", Описание);	
	
	Возврат ЭтотОбъект;
КонецФункции

Функция НовыйПортСервера() Экспорт
	ПортСервера = Новый Структура;
	ПортыСервера = Новый Массив;
	ПортСервера.Вставить("enum", ПортыСервера);
	ПараметрыСервера.Вставить("port", ПортСервера);
	
	Возврат ЭтотОбъект;
КонецФункции  

Функция ПортСервера(Порт) Экспорт
	ПортыСервера.Добавить(Порт);	 
	
	Возврат ЭтотОбъект;
КонецФункции       

Функция ПортПоУмолчанию(Порт) Экспорт
	ПортСервера.Вставить("default", Порт);	 
	
	Возврат ЭтотОбъект;
КонецФункции           

Функция ОписаниеПорта(Описание) Экспорт
	ПортСервера.Вставить("description", Описание);
	
	Возврат ЭтотОбъект;
КонецФункции
#КонецОбласти 

#Область Сервисы                                             
Функция Сервис(Имя, Представление, ПолноеОписание) Экспорт
	ИмяСервиса = СтрШаблон("/%1", Имя);
	СтруктураСервиса = Новый Структура;
	СтруктураСервиса.Вставить("summary", Представление);
	СтруктураСервиса.Вставить("description", ПолноеОписание);
	Сервисы.Вставить(ИмяСервиса, СтруктураСервиса);
		
	Возврат ЭтотОбъект;
КонецФункции

Функция Операция(ТипОперации, КраткоеОписание, ПолноеОписание) Экспорт
	МетодОбработки = СведенияОВнешнейОбработке.Команды.Добавить();
	МетодОбработки.Представление = СтруктураСервиса.summary + " (" + ТипОперации + ")";	
	МетодОбработки.Идентификатор = ИмяСервиса;	
	МетодОбработки.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовHTTPСервиса();
	МетодОбработки.ПоказыватьОповещение = Ложь;	
	МетодОбработки.Метод = ТипОперации;	     
	МетодОбработки.Префикс = Префикс;
		
	СтруктураОперации = Новый Структура;
	СтруктураОперации.Вставить("summary", КраткоеОписание);
	СтруктураОперации.Вставить("description", ПолноеОписание);
	СтруктураОперации.Вставить("operationId", МетодОбработки.Идентификатор); 
	СтруктураОперации.Вставить("parameters", Новый Массив);
	СтруктураСервиса.Вставить(ТипОперации, СтруктураОперации);
	
	Возврат ЭтотОбъект;
КонецФункции

Функция Параметр(Имя, Локация, Обязательный = Ложь, ВозможноПустой = Истина) Экспорт 
	СтруктураПараметра = Новый Структура;
	СтруктураПараметра.Вставить("name", Имя);
	СтруктураПараметра.Вставить("in", Локация);
	СтруктураПараметра.Вставить("required", Обязательный);
	СтруктураПараметра.Вставить("allowEmptyValue", ВозможноПустой);
	СтруктураПараметра.Вставить("explode", Истина);
	СтруктураОперации.parameters.Добавить(СтруктураПараметра);
	
	// TO DO 
	// Добавить возможностей параметров
	
	Возврат ЭтотОбъект;
КонецФункции  

Функция ТелоЗапроса(Описание, Обязательный = Ложь) Экспорт
	ТелоЗапроса = Новый Структура("description, required", Описание, Обязательный);
	СтруктураОперации.Вставить("requestBody", ТелоЗапроса);
	
	Возврат ЭтотОбъект;
КонецФункции   

Функция Контент(ТипКонтента) Экспорт  
	
	ОписаниеКонтента = Новый Структура;
	ОписаниеКонтента.Вставить("schema");
	
	СтруктураКонтента = Новый Соответствие;       
	СтруктураКонтента.Вставить(ТипКонтента, ОписаниеКонтента);
	
	ТелоЗапроса.Вставить("content", СтруктураКонтента);
	
	Возврат ЭтотОбъект;
КонецФункции
#КонецОбласти

Функция Создать() Экспорт   
	НовыйМетод = СведенияОВнешнейОбработке.Команды.Добавить();
	НовыйМетод.Представление = "swagger";	
	НовыйМетод.Идентификатор = "/swagger.json";	
	НовыйМетод.Использование = ДополнительныеОтчетыИОбработкиКлиентСервер.ТипКомандыВызовHTTPСервиса();
	НовыйМетод.ПоказыватьОповещение = Ложь;	
	НовыйМетод.Метод = "GET";
	НовыйМетод.Скрыть = Истина;
	НовыйМетод.Префикс = Префикс;
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	ЗаписатьJSON(Запись, ГлавныйОбъект);
	ТекстJSON = Запись.Закрыть();
	
	Возврат Новый Структура("ОписаниеСервиса, СведенияОВнешнейОбработке", ТекстJSON, СведенияОВнешнейОбработке);
КонецФункции
#КонецОбласти  